apply plugin:'groovy'
apply plugin:'eclipse'

repositories { maven { url "http://repo1.maven.org/maven2/" }}

configurations { jaxws }

sourceSets {
	main {
		java { srcDirs = []}
		groovy { srcDir 'src' }
	}
	test {
		java { srcDirs = []}
		groovy { srcDir 'test' }
		resources { srcDir 'resources' }
	}
}

task wsgen(dependsOn: compileGroovy)  {
	doLast{
		ant {
			taskdef(name:'wsgen',
			classname:'com.sun.tools.ws.ant.WsGen',
			classpath:configurations.jaxws.asPath)
			wsgen(keep:true,
			destdir: 'bin',
			sourcedestdir:'src',
			resourcedestdir:'resources',
			genwsdl:'true',
			classpath:sourceSets.main.classes.asPath,
			sei:'mjg.calc.service.GenericCalculator')
		}
	}
}
wsgen.onlyIf { !(new File('src/mjg/calc/service')).exists() }
classes.dependsOn wsgen

def groovyVersion = '2.1.4'
def spockVersion = '0.7-groovy-2.0'

dependencies {
	compile "org.codehaus.groovy:groovy-all:$groovyVersion"
	jaxws 'com.sun.xml.ws:jaxws-tools:2.2.7'

	testCompile "org.spockframework:spock-core:$spockVersion"
}
